apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION     = 34
def DEFAULT_BUILD_TOOLS_VERSION     = "28.0.3"
def DEFAULT_TARGET_SDK_VERSION      = 28

def DEFAULT_FIREBASE_SDK_VERSION    = "33.4.0"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace("com.transistorsoft.rnbackgroundgeolocation.firebase")
    }
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    
    defaultConfig {        
        minSdkVersion safeExtGet('minSdkVersion', 16)        
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

rootProject.gradle.buildFinished { buildResult ->
    if (buildResult.getFailure() != null) {
        try {
            String message = buildResult.getFailure().properties.get("reportableCauses").toString()

            if (message.contains("DexArchiveMergerException") || message.contains("DexIndexOverflowException")) {
                logger.log(LogLevel.ERROR, "")
                logger.log(LogLevel.ERROR, " ----------------------------------------------------------- ")
                logger.log(LogLevel.ERROR, "|         react-native-background-geolocation-firebase     | ")
                logger.log(LogLevel.ERROR, " ----------------------------------------------------------- ")
                logger.log(LogLevel.ERROR, "|                                                          | ")
                logger.log(LogLevel.ERROR, "| In your app/build.gradle, enable multidex:               | ")
                logger.log(LogLevel.ERROR, "|                                                          | ")
                logger.log(LogLevel.ERROR, "| android {                                                | ")
                logger.log(LogLevel.ERROR, "|    defaultConfig {                                       | ")
                logger.log(LogLevel.ERROR, "|        multiDexEnabled true  // <-- Add this             | ")
                logger.log(LogLevel.ERROR, "|    }                                                     | ")
                logger.log(LogLevel.ERROR, "| }                                                        | ")
                logger.log(LogLevel.ERROR, " ----------------------------------------------------------- ")

            }
        } catch (Exception exception) {

        }
    }
}

repositories{
    maven {
        url './libs'
    }
}

dependencies {
    def FirebaseSDKVersion = safeExtGet("FirebaseSDKVersion", DEFAULT_FIREBASE_SDK_VERSION);
    
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"

    // Import the Firebase BoM
    implementation(platform("com.google.firebase:firebase-bom:$FirebaseSDKVersion"))
    implementation "com.google.firebase:firebase-firestore"

    // tsfirebaseproxy.aar
    api(group: 'com.transistorsoft', name:"tsfirebaseproxy", version: '+')
}
